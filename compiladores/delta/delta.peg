//# Gram√°tica del lenguaje delta

comment = r'[/][/].*|[/][*](.|\n)*?[*][/]'

identifier = r'[a-zA-Z]\w*'

program = statement* expression EOF
statement = declaration / assignment / if / while
declaration = 'var' decl_variable (',' decl_variable)* ';'
decl_variable = identifier
assignment = lhs_variable '=' expression ';'
lhs_variable = identifier
if = 'if' expression block ('else' block)?
block = '{' statement* '}'
while = 'while' expression block
expression = additive ('&&' additive)*
additive = multiplicative (('+' / '-') multiplicative)*
multiplicative = unary (('*' / '/' / '%') unary)*
unary = ('+' / '-' / '!')* primary
primary = decimal / boolean / parenthesis / rhs_variable
decimal = r'\d+'
boolean = 'true' / 'false'
parenthesis = '(' expression ')'
rhs_variable = identifier